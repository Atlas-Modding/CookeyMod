plugins {
	id 'fabric-loom' version '0.5-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

configurations {
	includeClasspath
}

dependencies {
	String clothConfig = "me.shedaniel.cloth:config-2:${project.cloth_config_api_version}"
	String toml4j = "com.moandjiezana.toml:toml4j:${project.toml4j_version}"
	Dependency fabricResourceLoader = fabricApi.module("fabric-resource-loader-v0", project.fabric_version) as Dependency

	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings minecraft.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modApi(clothConfig) {
		exclude(group: "net.fabricmc.fabric-api")
	}
	include clothConfig

	implementation toml4j
	includeClasspath toml4j

	modImplementation fabricResourceLoader
	include fabricResourceLoader

	modImplementation("io.github.prospector:modmenu:${project.modmenu_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
}

// Custom Manifest and Intermediary mappings declaration
minecraft {
	customManifest = "https://gist.githubusercontent.com/rizecookey/4c6142baaccc3875f9b227fe22f2ace5/raw/c8ed74b19f7a5315813c9d4b199798b692a8f359/1.16_combat-6.json"
	intermediaryUrl = { mcVersion ->
		return "https://maven.combatreforged.com/net/fabricmc/intermediary/${mcVersion}/intermediary-${mcVersion}-v2.jar";
	}
	accessWidener "src/main/resources/cookeymod.accesswidener"
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	archiveClassifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
	from {
		configurations.includeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
	manifest {
		attributes 'Main-Class': 'net.rizecookey.updater.Updater'
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	repositories {
		// mavenLocal()
	}
}